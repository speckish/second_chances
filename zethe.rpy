label zethe:
    scene black with fade
    yuki None
    "Over the next few days, Yuki and Zethe met up in Yuki's dorm to discuss the project."
    scene bg yukis_dorm day
    show zethe
    with fade
    z "No matter how many times I visit, I find this place too narrow."
    yuki angry
    "Yuki rolled his eyes."
    y "Okay, Mr. Rich Guy."
    yuki
    show zethe angry at seated with move
    "Zethe grumbled and settled in the chair next to Yuki."
    yuki blush
    show zethe neutral
    "Yuki adjusted his tie. Two guys in such tiny dorm got hot very quickly."
    yuki
    z "Since we are creating a phoenix, we will require—"
    yuki angry
    y "We're not making a phoenix."
    show zethe angry at yell
    z "Why not?!"
    yuki sad
    "Yuki rested his head in his hands."
    y "Are we going to go over this again..."
    show zethe neutral
    z "It's not like you're any better."
    yuki angry
    y "You don't understand. A spell that dispenses healing magic over a wide range is far superior!"
    show zethe angry
    z "There's already a spell for that!"
    yuki angry blush yell
    y "But it would be in the shape of a teddy bear!"
    show zethe angry blush
    yuki angry blush
    z "That's commpletely useless!"
    "After they argued some more, they finally decided on a compromise: A phoenix that healed over a wide range."
    show zethe neutral -blush
    yuki
    z "The amount of components we would have to keep in mind..."
    y "That's why we're on it together."
    show zethe sad
    "Zethe went uncharacteristically quiet."
    yuki happy
    "Since they'd finally agreed on a subject, Yuki smiled."
    yN "This might actually work out..."

    show bg yukis_dorm evening
    show zethe
    with fade
    yuki
    "Yuki and Zethe sat down together and researched magic techniques."
    "Yuki went through many different spell books at a rapid pace."
    show zethe shocked
    z "How do you read so fast?"
    yuki sad
    "Zethe looked genuinely puzzled."
    yuki
    y "Practice."
    show zethe neutral
    yuki sad blush
    "Yuki couldn't tell him he had another decade of doing nothing but reading books in an empty cabin."
    yuki sad
    "Despite his best efforts to cover it up, Zethe had difficulty reading for long periods of time."
    y "Zethe."
    z "Hmm?"
    yuki sad
    y "How about this: I read theoretical techniques for now, while you finish sketching possible summoning circles."
    show zethe sad
    yuki
    "Zethe sighed in relief."
    z "Alright."
    show zethe neutral
    "Zethe pulled out a paper and began to draw several intricate patterns in a flash. Something Yuki could never do."
    "Sighing, Yuki went back to his reasearch. A heading in the book caught his eye."
    yuki sad
    yN "Magic from heartbreak."
    show zethe sad
    "Yuki glanced over at Zethe who was engrossed in his work. He felt his heart skip a bit."
    show zethe neutral
    yuki shocked blush
    yN "Am I... feeling something for him?"
    yuki shocked
    "From the beginning, Yuki tried to deny his feelings for Zethe. He tried to push it down."
    "But now... now it was something he couldn't deny anymore."
    yuki shocked blush
    "Looking over at his chiseled face, his beautiful eyes..."
    yuki sad blush
    "Then it struck Yuki. Zethe didn't care for him. He was in love with Quinn. Just as he himself was in his previous life."
    yuki sad
    # chair scrape
    show zethe shocked
    "Yuki frowned. It was too much. He got up suddenly, startling Zethe."
    yuki sad
    y "I-I'll be going for a bit."
    show zethe angry
    z "What?"
    # anim grab
    "Zethe grabbed Yuki's arm. His eyes flit over to the passage Yuki just read and his cheeks heated."
    yuki shocked
    y "..."
    yuki shocked blush
    yN "This is mortifying!"
    yuki sad
    "He yanked his arm away."
    y "S-Sorry. I'll be going for a bit."
    call .kiss from _call_zethe_kiss
    play music "audio/Rest-In-Peace_loop.ogg" fadein 1.0
    call research from _call_research
    play music "audio/Crasher_loop.ogg" fadein 1.0
    call .fun from _call_zethe_fun
    play music "audio/Nostalgia_loop.ogg" fadein 1.0
    call finalMother from _call_finalMother
    play music "audio/Rest-In-Peace_loop.ogg" fadein 1.0
    call .ending from _call_zethe_ending
    return

label .kiss:
    yuki shocked
    "But then, Zethe grabbed Yuki's arm and pulled [him] closer."
    z "Yuki! Listen to me."
    yuki sad
    "Yuki felt a pain in [his] heart. [He] knew that Zethe didn't love [him]... he was in love with Quinn. That was how it was."
    yuki sad blush
    "But when Zethe looked at [him] with such fire in his eyes, Yuki couldn't look away."
    yuki sad
    y "What is it?"
    "Yuki steeled [his] nerves and looked at Zethe right in the eyes."
    show zethe sad
    z "I..."
    "For a brief moment, Zethe's gaze dropped to Yuki's lips."
    y "What?"
    z "I..."
    yuki shocked
    "Zethe raised his fingers to Yuki's face and caressed it gently."
    yuki shocked blush
    yN "(H-Huh?!)"
    show zethe sad blush
    "Zethe's gentle touch turned into a tight grasp as he pulled Yuki's face even closer."
    z "You're just so...."
    scene zethekiss with fade
    pause 1.5
    yuki None
    "And kissed [him]."
    "Yuki's mind went blank as Zethe's lips touched [his]. They were soft and warm, with a hint of spice on top."
    yuki blush
    y "Mmph...!"
    yuki None
    "Without realizing it, Yuki slid [his] arms around Zethe's waist and pulled them closer."
    "Yuki felt Zethe's muscles flex under [his] touch,
    and felt a rush of pleasure go straight to [his] head."
    z "Mm..."
    "Zethe tilted his head to deepen the kiss, and Yuki returned the gesture."
    "Yuki began to feel lightheaded and breathless. It felt amazing, yet—"
    "Suddenly, Zethe pushed Yuki's shoulders away, breaking the kiss. Yuki licked [his] lips, missing the feeling already. Zethe's eyes hungrily traced the motion."
    scene bg yukis_dorm evening
    yuki blush
    show zethe neutral blush
    with fade
    y "So..."
    "Yuki started to say something, but was quickly interrupted."
    yuki shocked blush
    z "I love you."
    yN "W-What?!"
    show zethe sad blush
    z "I love you, Yuki. There's just something about you that drives me crazy."
    yuki shocked blush sweat
    "Yuki felt shocked, but [his] brain was too fried to make any meaningful sounds."
    show zethe neutral blush
    y "B-Buh?"
    yN "(Great job sounding smart there...)"
    yuki sad blush
    "Although [hed] expected a snarky remark, Zethe's face held only a smile."
    show zethe sad blush
    z "At first, I found you infuriating. I hated everything about you."
    show zethe shocked blush
    "Yuki's chest tightened. Zethe immediately noticed the shift in [his] demeanor."
    yuki blush
    z "B-But quickly, I became unable to take my eyes of you. When you were around, I could not stop looking at you. When you were gone, I was always thinking of you."
    show zethe neutral blush
    z "I wanted you to be mine. And in turn..."
    "Zethe sucked in a sharp breath."
    show zethe sad blush
    z "I wanted to be yours."
    "Zethe tenderly tapped his forehead to Yuki's."
    z "Please...!"
    "His words were so desperate that it pulled at Yuki's heartstrings."
    call .accept from _call_zethe_accept
    return

label .accept:
    show zethe neutral blush
    "Yuki took a deep breath and steadied [himself]."
    yuki blush
    y "I feel the same about you, Zethe."
    show zethe happy blush
    "Zethe's eyes lit up."
    z "Really?"
    "His face continued such joy it was impossible for Yuki to resist. [He] moved [his] hand over Zethe's and linked their fingers together."
    y "Yes, I meant it. I..."
    yuki sad blush
    "Yuki felt his throat tighten. The words \"I love you\" were too much at the moment."
    "So [he] settled for the strongest words [he] could manage."
    yuki blush
    y "I care about you too. A lot."
    show zethe neutral blush
    "Zethe's eyes softened and become moist."
    show zethe sad blush
    z "I can't... I can't believe this... I just..."
    yuki blush happy
    "To save him from his trouble with words, Yuki gave Zethe a quick kiss on the lips."
    show zethe shocked blush
    z "..."
    show zethe happy blush
    z "Today, you have made me the second happiest man on this planet."
    yuki
    show zethe neutral -blush
    y "The {i}second{/i} happiest?"
    show zethe happy
    yuki shocked
    z "Yes, because I made you the happiest one."
    yuki sad
    "Zethe flashed his cocky smile, but it twitched with a hint of nervousness. Yuki felt [his] heart melt."
    yuki happy
    yN "(Is this what being in love is like...?)"
    scene black with fade
    return

label .fun:
    yuki None
    "The next few weeks were incredibly exciting."
    "Zethe took Yuki to incredibly wild, exotic places. They went to magic shows, concerts, and even live sporting arenas."
    scene bg yukis_dorm night with fade
    "Yuki and Zethe stumbled back into Yuki's dorm, gasping for air."
    yuki happy
    y "You... are... crazy."
    show zethe at offscreenleft
    pause 0.0
    show zethe happy at center with move
    "Zethe smirked tiredly."
    z "Not as... crazy.. as... you...!"
    yuki
    "Yuki collapsed on the bed, Zethe followed suit next to him."
    y "Move. This bed is not big enough."
    show zethe neutral
    z "You move."
    yuki happy
    y "This is my dorm!"
    show zethe happy
    z "Don't care."
    yuki
    "Yuki glared at Zethe, who was unaffected by it."
    y "You're so... ugh."
    yuki shocked blush
    "Zethe leaned over and kissed Yuki. Yuki felt his exhaustion melt away and leaned into it."
    yuki happy blush
    show zethe happy blush
    y "Mm..."

    "A rush of pleasure rushed to Yuki's head as he slowly broke the kiss."
    yuki blush
    y "...You're lucky I'm so forgiving."
    show zethe happy -blush
    z "Sure."
    yuki angry
    y "What were you thinking, setting the statue in Main Square on fire?!"
    show zethe shocked
    z "That was an accident!"
    yuki
    y "Hmph. That would've never happened if I was there."
    show zethe neutral
    z "Suuure."
    "Zethe dragged out the words slowly."
    yuki angry
    y "You..."
    show zethe angry blush
    "Yuki pinched Zethe's nose."
    z "Ow!"
    yuki
    y "You deserve that. My feet hurt."
    show zethe happy -blush
    yuki blush
    "Zethe just laughed and pulled Yuki into a kiss."
    yuki happy blush
    "Yuki obliged. He was happier than he'd ever been in his entire lifetime."
    yuki sad sweat
    show zethe shocked
    "Recalling the past, Yuki pulled back sharply."
    z "What's wrong?"
    show zethe sad
    "Yuki rubbed his forehead."
    yuki
    y "I-It's nothing. Sorry just... a little tired today."
    "Zethe looked suspicious but didn't press the issue."
    show zethe neutral
    z "If you say so..."
    yuki sad sweat
    "Although Yuki was incredibly happy, a sense of fear bubbled inside. But he ignored it for now."
    yuki neutral
    y "I meant it. Come here."
    show zethe neutral blush
    "Yuki pulled Zethe into tight hug."
    yuki happy blush
    show zethe happy blush
    yN "Everything will work out. I swear it."
    return

label .ending:
    yuki shocked
    show zethe shocked blush with dissolve
    "Zethe stood in the doorway, his face flushed from exertion."
    show zethe angry blush
    z "What are you doing, you complete and utter fool! Stop this madness!"
    yuki shocked sweat
    "Yuki looked at him, shocked."
    y "...What are you doing here?"
    show zethe shocked -blush
    z "I saw your notes, you idiot! Stop this! You'll DIE!"
    yuki sad
    "Yuki narrowed his eyes."
    y "I knew it. You think I'm too weak to do it, right?"
    show zethe angry
    "Zethe raised his fist in frustration."
    z "No, I {i}know{/i} that this is a futile plan!"
    yuki angry
    "Yuki felt agitated. This was supposed to be the final spell, the one that would save his mother."
    "He glanced at Zethe. If he didn't cast the spell now, it would be too late."
    "Yuki decided..."
    menu:
        "To cast the spell":
            jump .bad
        "Not to cast the spell":
            jump .good
    return

label .bad:
    yuki sad
    show zethe shocked
    "Yuki squeezed his eyes shut."
    y "I have to do this."
    "He took in a deep breath and began chanting."
    y "{font=Monaco.dfont}w̸̧͖̩͕̰̋̈̂̈́́͗̽̇͝͝a̸̤̮̹̳̭͖͂̐̋̿̑̅̆̒͌͊̚͠͠ẹ̴̛͐́̑̈́̐̅̿͠ȑ̵̨͚͈͙̻͍̘̪̭̟̩̦̟̓̄͒͛̌͗̑̔f̴̡̟̪͎̦̣͔̖̫̯͈́͆́́̈́̈́̋̏͐́̓̐͛̉ǧ̷̜̬̽̒̎͗̃͑̋̿́̅͆͝d̷͙̹͈͈͚̣̳̰̒̍͂̉͒͑͋͑̈́̋͝ͅt̷̡̟̯̜̹̲̜͙̦̽̄̍̀͠f̷̧̨͕̤͒̀h̷̝̖͙͕͔̰̣͓̿̅̀̓̃̄͘͜͝͝ỳ̶͚̬͙̹̏̄̇͋̃̽̔̏͝͝͝ͅġ̶̥͖͖̻̞͖͇̹̹̍̇j̶͎̼̹̜͕̟̭̼̦͊̓̽̿̔̒̔̀k̵̨̳̘̠̥̳͚͌́̒̆͜͝ḣ̷̨̖͔̣́̅̒̿̍̔͋̂̀̽̈̒͘ͅg̸̙̜̟̼̺͍̫̦͗̐f̶̣̰̖͈̔̎̐d̶̤̱̯̮͇̦̜̾̆̉̈́͌̀͛͋͘͝s̷̨̨̖̫̀̐̐̆̒͆̄a̶̲̞̖͓̬͈͈̻̔͒͊̾͌̕͜s̴̡̜̫̼̼̔͑c̷̨͕̙̺̻̜̲̦̠̥͖͎͋͠d̸̛̤̬̹̭͙͂̀͑͐̒̑̽͛́̿͗̄̊͘v̶̢̛͉̠̳͚̘̀̈̊̈́̂̽̌́̈́̓̑̊͘̚b̵̡̧̛͚̰̗̘̼͔͇̜̦̮̉͒̾̔̽̀͘͠͝ͅṅ̶̥̯͉̥̪̜̓̈̄̎͒̅̀͋̈́͝ͅ ̶̨̡̙̘̤̭̖̥̻̥̖̗̈́͆͊̈́̎͐̅̈̄͘͘͠{/font}"

    show zethe shocked blush
    z "NO! YUKI!!"
    scene black with fade
    yuki None
    "Yuki squeezed his eyes shut. He couldn't bear to see Zethe's face."
    "He braced for the inevitable flare of pain, but nothing came."
    yuki shocked sweat
    yN "What...?"
    yuki angry sweat
    yN "Don't tell me...! It failed?"
    "Yuki slowly opened one eye. Then the other."
    scene zethe_bad_end with fade
    yuki None
    "Zethe laid on the floor next to Yuki, unmoving."
    y "Zethe...? ZETHE?!"
    "Yuki grabbed Zethe's shoulders and cradled him in his arms."
    y "What did you do... Don't tell me..."
    "Yuki felt hot tears pool up in his eyes."
    z "YOU MORON!"
    "Zethe cracked a tiny smile."
    y "Even now... you're... so..."
    y "No... why did you take on the spell's recoil?!"
    z "I... had... to. I had to protect..."
    "Zethe's eyes fluttered. Yuki felt Zethe's soul flee."
    y "NO!!!"
    "Yuki screamed in agony. His heart beat erratically, and his throat was dry."
    y "I love you! I love you, Zethe. You can't...!"
    z "I... love you... too. Forever."
    y "Zethe?!"
    "When Zethe didn't respond, Yuki's instincts kicked in. He moved healing magic through his palms."
    play sound "audio/yuki_magic_chime.mp3"
    y "{i}Maior mederi vulneribus!{/i}"
    "In the back of his mind, Yuki knew it was futile. But he couldn't stop himself."
    play sound "audio/yuki_magic_chime.mp3"
    y "{i}Maior mederi vulneribus!{/i}"
    z "Yuki..."
    "Zethe's voice was far away. His skin turned pale."
    y "Just hang in there, okay?! It will be—"
    "Yuki stopped himself."
    yN "His soul is..."
    "It was gone. Zethe's soul had passed."
    y "NO!!"
    "Yuki cried, holding Zethe's limp body close."
    scene bg yukis_cabin_inside closed_door with fade
    "Yuki sat in his cabin."
    show cat neutral:
        align(0.5, 0.5)
    with dissolve
    cat "..."
    "He stared off into the window."
    "Although the spell had saved his Mother, the shock of losing Zethe changed Yuki forever."
    "He was a broken man."
    yuki sad
    y "..."
    "Yuki had no emotion left in him."
    "But still, he felt a void in his chest. One that would never be filled."
    "A picture of Zethe was on the table, next to Yuki."
    yuki sad blush
    y "..."
    yuki sad
    y "Who... who am I?"
    "Yuki clutched his head in pain."
    yuki angry
    y "AAAAAAAAAH!!!"
    "His body shook relentlessly."
    yuki angry blush
    y "I... I don't...!!"
    "He stared at his hands."
    yuki sad blush
    y "Who... am I?"
    $ yuki_name = "???"
    yN "What... what is my name?"
    yuki shocked
    "{font=Menlo.ttc}████{/font} stared at his palms."
    y "My... my name is..."
    "In terror, {font=Menlo.ttc}████{/font} looked around the room."
    y "I'm... I'm...!"
    yuki shocked blush
    "Realizing something, {font=Menlo.ttc}████{/font} dropped his palms."
    y "It doesn't matter... does it. Who I am..."
    yuki sad blush
    "{font=Menlo.ttc}████{/font} felt painful emotions in his chest surface. But he couldn't remember why he felt it. Or what made him feel that way."
    "All {font=Menlo.ttc}████{/font} knew was, staying among others would only cause everyone more suffering."
    yuki sad
    yN "This is for the best."
    cat "..."
    show cat smug
    cat "Unable to see himself repeating past mistakes. Making himself miserable despite a second chance."
    show cat smirk
    cat "Humans are truly pathetic creatures."
    return

label .good:
    play music "audio/tea-time_loop.ogg" fadein 1.0
    yuki
    "Yuki stared at Zethe."
    yuki sad
    "Finally, Yuki sighed and dropped his hands."
    y "...Very well."
    show zethe neutral
    "Zethe rushed into the room and tightly grabbed Yuki."
    yuki shocked sweat
    y "Can't... breathe..."
    show zethe sad
    "Zethe loosened his hold on Yuki slightly."
    show zethe sad blush
    z "I... I am so glad...!"
    yuki sad
    "Yuki looked up at Zethe, his eyes filled with tears."
    yuki shocked blush
    y "I-I...!"
    "Zethe grabbed the back of Yuki's waist and pulled him close."
    show zethe neutral blush
    z "It is okay. It is okay now."
    yuki sad blush
    "Zethe's kind words felt strange to Yuki's ears. But he accepted them."
    y "I-I just wanted her to live!"
    show zethe sad -blush
    z "I know! I know. But as a magician, you should know better than anyone Yuki... that death is—"
    yuki angry
    y "Inevitable! I know. Of course I do. I also know that she's going to a better place and—"
    z "But you wanted her to live."
    yuki sad
    "Zethe's eyes were teary, filled with understanding. Yuki couldn't bear looking at him, and buried his face in Zethe's chest."
    "Zethe roughly stroked Yuki's hair."
    show zethe sad blush
    z "I love you. So you cannot die on me, okay?!"
    "Zethe tried to keep his voice steady, but a tremble betrayed his desperation."
    yuki blush
    y "You're a strange one."
    show zethe angry blush
    z "S-Shut up. Don't try to act tough. I know... I know this is hard on you."
    yuki sad blush
    show zethe sad blush
    "Yuki began to cry."
    y "I... I just..."
    z "It is fine. Whether you will it or not, you are stuck with me forever."
    show zethe blush
    yuki blush
    z "Let us go now, before it becomes suspicious."
    "Zethe and Yuki left the lab. Yuki's body was physically drained, but Zethe carried him back."
    scene bg mountain_forest day with fade
    yuki None
    "Five years later..."
    "Yuki walked up the slope of a hill."
    yuki blush
    y "{i}pant... pant...{/i}"
    "Yuki wiped the sweat off his brow."
    yuki sad blush
    yN "Just a little longer...!"
    "Finally, Yuki arrived at the summit."
    yuki blush
    y "Phew..."
    "He collapsed onto the ground."
    yuki shocked
    yN "No, wait! I have to get something!"
    yuki
    y "...I can stay here a little longer, though."
    scene black with fade
    yuki None
    "Yuki closed his eyes."
    "Yuki's mother was proclaimed dead just a fews hours after the incident on that fateful day."
    "Although Yuki couldn't make the funeral, he visited her grave immediately after the semester ended."
    "Since the successful completion of their project, Yuki became far more interested in spells that combined practicality and flashiness."
    "After graduation, he partnered with Zethe to start a travelling magician act."
    "They gained astonishing popularity, but their relationship remained mostly the same."
    "One major aspect that remained unchanged was their competitiveness."
    scene bg mountain_forest day with fade
    yuki sad
    yN "I know I should hurry up and get the ingredient but..."
    "Yuki wasn't used to physical activity. Not that he'd admit it."
    scene black with fade
    y "I'll just rest my eyes for... a little while..."
    pause 0.5
    scene bg mountain_forest night with fade
    yuki shocked sweat
    y "AAHHH!"
    "Yuki awoke from his slumber, suddenly remembering why he was here."
    yuki sad sweat
    y "Oh no, it's dark!"
    "Yuki stood up hastily and glanced around. He began to rummage through the bushes."
    yuki sweat
    yN "The secret ingredient to make the special phoenix should be here..."
    yuki sad sweat
    "But no matter how hard Yuki looked, he couldn't find it."
    "Feeling discouraged, he turned around to walk back."
    yuki shocked startle
    z "Yuki?! YUKI!!"
    yuki shocked
    yN "What is Zethe doing here?!"
    "Yuki froze."
    z "Where are you...? Yuki?!"
    yuki
    y "I-I'm here..."
    "Zethe didn't hear him, so Yuki had to try something different."
    yuki sad
    play sound "audio/yuki_magic_chime.mp3"
    y "{i}Surge caudis titionum fumigantium{i}"
    yuki
    "A small fire erupted from his palm."
    play sound "audio/steps.mp3"
    "Zethe immediately caught sight of it and rushed over."
    show zethe shocked blush at center with dissolve
    z "Yuki!! I was so worried... Oh..."
    "Zethe grabbed Yuki in a tight hug."
    yuki blush
    y "Can't... breathe..."
    "Zethe broke the hug."
    show zethe angry
    z "I was looking for you!"
    yuki sad
    "Yuki sighed."
    y "I was looking for the Ember of Laudia..."
    show zethe shocked
    "Zethe froze."
    z "The... plant to summon the great phoenix?"
    y "Yeah."
    show zethe happy
    "Zethe's mouth stretched in a wide smile."
    yuki
    y "Why are you acting so weird?"
    show zethe happy blush
    z "I-I am not."
    yuki angry
    "Yuki narrowed his eyes."
    show zethe happy blush sweat
    z "It is just..."
    yuki shocked
    "Zethe reached into his pant pocket and produced a small, red flower."
    yuki shocked sweat
    y "This is..."
    show zethe neutral blush sweat
    z "The Ember of Laudia, yes. I was planning on summoning Laudia for our fifth anniversary."
    y "Don't tell me..."
    show zethe happy -blush -sweat
    "Zethe's cocky smile told Yuki everything."
    z "That's right, I beat you to it!"
    yuki sad sweat
    "Yuki put his face in his hands."
    y "Not again!"
    "For last years anniversary, Zethe had gotten to the Crystal Flower first as well."
    yuki shocked
    z "Just give up already."
    yuki angry
    show zethe shocked at startle
    "Yuki pinched Zethe's nose."
    z "Ow!"
    y "Take that!"
    show zethe neutral
    "Zethe rubbed his nose."
    show zethe happy
    z "I did win."
    yuki sad
    "Yuki huffed."
    y "Fine. What do you want?"
    show zethe shocked
    "Zethe's eyes darted from side to side nervously."
    y "..."
    "Yuki had a strange feeling."
    show zethe sad at seated with move
    yuki shocked
    "Then, Zethe got down on one knee."
    yuki shocked sweat
    yN "No way...!"
    show zethe sad blush
    z "I want you to marry me."
    yuki shocked sweat blush
    "Yuki's brain was fried."
    y "W-What?"
    show zethe angry blush
    z "You heard me!"
    show zethe neutral blush
    "Yuki knew Zethe was using his bravado to cover up his embarrasment."
    yuki happy blush
    show zethe shocked blush
    y "Yes. Of course I will, stupid!"
    show zethe shocked blush at center with move
    "Zethe grabbed Yuki in a tight hug and kissed him."
    show zethe happy blush
    z "I know."
    yuki angry blush
    y "You...!"
    yuki shocked blush
    "Zethe cut him off with another kiss."
    yuki happy blush
    y "Mmph!"
    "Finally, Zethe released him, panting."
    z "Just... so you know, this isn't the end of your part of the bet."
    y "What? I thought it was!"
    z "You thought that was it?"
    "Zethe licked his lips."
    z "Don't think you can get off that easy."
    "Yuki gulped, his heart pounding loudly."
    yuki None
    show cat at offscreenright
    pause 0.0
    show cat smug:
        align (0.5, 0.5)
    show zethe at offscreenleft
    with move
    "Hidden in a bush, Aurelia yawned."
    show cat neutral
    cat "Humans sure are stupid..."
    show cat smirk
    cat "But isn't that what makes them special?"
    return
